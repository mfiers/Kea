start = 
  samtools_view | 
  samtools_flagstat | 
  samtools_index | 
  samtools_merge | 
  samtools_sort | 
  samtools_view_00 | 
  samtools ;

samtools_view = "samtools" /\s+/ "view" { /\s+/ viewoptions} /\s+/ inputfile { /\s+/ regions} $ ;
samtools_flagstat = "samtools" /\s+/ "flagstat" /\s+/ inputfile $ ;
samtools_index = "samtools" /\s+/ "index" /\s+/ inputfile $ ;
samtools_merge = "samtools" /\s+/ "merge" /\s+/ outputfile /\s+/ inputfile { /\s+/ inputfile} $ ;
samtools_sort = "samtools" /\s+/ "sort" { /\s+/ sortoptions} /\s+/ inputfile /\s+/ outprefix $ ;
samtools_view_00 = "samtools" /\s+/ "view" $ ;
samtools = "samtools" $ ;

sortoptions = (
  sortoptions_flag
  | "-l" /\s+/ int
  | "-@" /\s+/ int
  | "-m" /\s+/ string
  ) ; 

sortoptions_flag = "-" { /n/
        | /f/
        | /o/ }+;

viewoptions = (
  viewoptions_flag
  | "-@" /\s+/ int
  | "-L" /\s+/ bedoverlap
  | "-t" /\s+/ file
  | "-T" /\s+/ file
  | "-o" /\s+/ file
  | "-R" /\s+/ file
  | "-f" /\s+/ int
  | "-F" /\s+/ int
  | "-q" /\s+/ int
  | "-l" /\s+/ string
  | "-r" /\s+/ string
  | "-s" /\s+/ float
  ) ; 

viewoptions_flag = "-" { output_bam+:/b/
        | /h/
        | /H/
        | /S/
        | /u/
        | /1/
        | /x/
        | /X/
        | /c/
        | /B/
        | /\?/ }+;


(* files *)
outputfile = outputfile+:/\S+\.bam/ ; 
inputfile = inputfile+:/\S+\.[bs]am/ ; 
bedoverlap = bedoverlap+:/\w+/ ; 


(* other *)
regions = regions+:/\S+/ ; 
outprefix = outprefix+:/\S+/ ; 


(* Misc *)
file = /\S+/ ;
int = /[0-9]+/ ;
float = /[0-9\.]+/ ;
string = ( /"[^"]*"/ | /'[^']*'/ | /\S+/ ) ;

